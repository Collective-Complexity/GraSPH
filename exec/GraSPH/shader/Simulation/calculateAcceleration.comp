#version 450 core
// we have to use a fixed work group size here

#include "common.glsl"
#include "kernel.glsl"

layout(binding=PARTICLE_POSITION_BUFFER_BINDING,std430) buffer ParticlePositions
{
    vec4 positions[];
};

layout(binding=PARTICLE_VELOCITY_BUFFER_BINDING,std430) buffer ParticleVelocities
{
    vec4 velocities[];
};

layout(binding=PARTICLE_ACCELERATION_BUFFER_BINDING,std430) buffer ParticleAccelerations
{
    vec4 accelerations[];
};

layout(binding=PARTICLE_HYDRO_BUFFER_BINDING,std430) buffer ParticleHydro
{
    vec4 hydro[];
};

layout(binding=PARTICLE_SMLENGTH_BUFFER_BINDING,std430) buffer ParticleSmlength
{
    float smlength[];
};

layout(local_size_x=WGSIZE,local_size_y=1,local_size_z=1) in;

uniform float eps_factor2;
uniform float a; // entropy of the fluid
uniform float alpha; // controle viscosity

shared vec4 pos[gl_WorkGroupSize.x];
shared vec4 hyd[gl_WorkGroupSize.x];
shared float h[gl_WorkGroupSize.x];
shared vec4 vel[gl_WorkGroupSize.x];

// This shader updates a particles acceleration by interacting with all other particles,
// using shared memory to speed up memory access
void main()
{
    // there can be multiple threads per particle, so figure out where we start calculating
    const uint startTile = TILES_PER_THREAD * uint(gl_GlobalInvocationID.x / NUM_PARTICLES);
    const uint idxi = gl_GlobalInvocationID.x % NUM_PARTICLES;

    // cache my particle attributes in local memory
    const vec4 hydroi = hydro[idxi];
    const vec4 posi = positions[idxi];
    const float hi = smlength[idxi];
    const vec4 veli = velocities[idxi];

    vec3 acc = vec3(0); // lets sum up the acceleration here
    float maxVsig = 0; // needed for the timestep criterion

    // calculate some values that are the same for all loop iterations
    const float pod2i = (hydroi.PRESSURE / (hydroi.DENSITY * hydroi.DENSITY));
    const float hiSpikyGradFactor = spikyGradFactor(hi);

    // loop over tiles in a row for as many tiles one thread is configured to calculate
    // calculate the interactions of a particle with gl_WorkGroupSize.x particles in one tile
    // repeat until all interactions in all tiles are calculatedsss
    for(uint tile = 0; tile < TILES_PER_THREAD; tile++)
    {
        // fill fields in shared memory
        const uint tileStartIndex = gl_WorkGroupSize.x * (startTile + tile); // the index in the global buffer where this tile begins
        const uint idx= tileStartIndex + gl_LocalInvocationID.x;
        pos[gl_LocalInvocationID.x] = positions[idx];
        hyd[gl_LocalInvocationID.x] = hydro[idx];
        h[gl_LocalInvocationID.x] = smlength[idx];
        vel[gl_LocalInvocationID.x] = velocities[idx];

        memoryBarrierShared();
        barrier();

        // calculate the row up to here use sink function if this is a sink particle
        for(uint j=0; j<gl_WorkGroupSize.x; j++) // go over everything in this tile
        {
            const vec4 posj = pos[j];
            const float hj = h[j];
            const vec4 hydroj = hyd[j];
            const vec4 velj = vel[j];

            const vec3 rij = posi.POSITION - posj.POSITION; // vector from i to j
            const float r2 = dot(rij,rij);
            const float dist = sqrt(r2); // distance from i to j

            // gravity
            acc +=  posj.MASS * -rij / sqrt(pow(r2+(hi*hj*eps_factor2),3));

            // pressure
            const float pod2j = hydroj.PRESSURE / (hydroj.DENSITY * hydroj.DENSITY);

            const vec3 gradi = WspikyGrad(rij,dist,hi,hiSpikyGradFactor);
            const vec3 gradj = WspikyGrad(rij,dist,hj);

#ifdef SPRINGEL_CORRECTION
            const float fdi = 1.0/(1.0+ ((hi/(3*hydroi.DENSITY)) * sum.DRHO_DH));
            const float fdj = 1.0/(1.0+ ((hi/(3*hydroi.DENSITY)) * sum.DRHO_DH));
#else
            const float fdi = 1;
            const float fdj = 1;
#endif
            acc -= posj.w * (hydroi.w*pod2i* gradi + hydroj.w*pod2j* gradj);

            // viscosity
            float wij = dot(veli.VELOCITY - velj.VELOCITY, rij)/dist;
            wij = (wij <0) ? wij : 0;
            const float vsig = hydroi.SPEED_OF_SOUND + hydroj.SPEED_OF_SOUND - 3.0*wij;
            const float rhoij = (hydroi.DENSITY + hydroj.DENSITY)*0.5;
            const float II = -alpha * 0.5 * vsig * wij / rhoij;

            maxVsig = max(maxVsig,vsig);
            acc -=  posj.w  * II * (gradi+gradj)*0.5f;
        }

        memoryBarrierShared();
        barrier();
    }

    accelerations[gl_GlobalInvocationID.x] = vec4(acc,maxVsig);
}